import static org.springframework.boot.gradle.SpringBootPluginExtension.LayoutType.*


buildscript {
    repositories {
        mavenLocal()
        maven { url "https://nexus.ala.org.au/content/groups/public/" }
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "org.grails.plugins:hibernate5:${gormVersion-".RELEASE"}"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:2.14.4"
    }
}

plugins {
    id "com.gorylenko.gradle-git-properties" version "2.1.0"
}

version "1.0.3"
group "au.org.ala"

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"war"
apply plugin:"org.grails.grails-web"
apply plugin:"org.grails.grails-gsp"
apply plugin:"asset-pipeline"
apply from: 'https://raw.githubusercontent.com/AtlasOfLivingAustralia/travis-build-configuration/master/travis_grails_publish.gradle'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    maven { url "https://nexus.ala.org.au/content/groups/public/" }
    maven { url "https://repo.grails.org/grails/core" }
    maven { url "https://repository.gbif.org/content/groups/gbif/" }
}

// Inplace plugin config
// Set to true it needed only for development
boolean inplace = false

if(inplace) {
    grails {
        exploded = true
        plugins {
//            compile project(':ala-admin-plugin')
            compile project(':ala-auth-plugin')
        }
    }
}

// To prevent: http://hg.openjdk.java.net/jdk8/jdk8/jaxp/rev/5c84d4a878f1
// when using gbif-doi datacite xml validator
def withoutXerces = { exclude group: 'xerces', module: 'xercesImpl' }

dependencies {
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.grails:grails-web-boot"
    compile "org.grails:grails-logging"
    compile "org.grails:grails-plugin-rest"
    compile "org.grails:grails-plugin-databinding"
    compile "org.grails:grails-plugin-i18n"
    compile "org.grails:grails-plugin-services"
    compile "org.grails:grails-plugin-url-mappings"
    compile "org.grails:grails-plugin-interceptors"
    compile "org.grails.plugins:cache"
    compile "org.grails.plugins:async"
    compile "org.grails.plugins:scaffolding"
    compile "org.grails.plugins:events"
    compile "org.grails.plugins:hibernate5"
    compile "org.hibernate:hibernate-core:5.1.5.Final"
    compile "org.grails.plugins:gsp"
    compile "org.gbif:gbif-doi:2.9"
    console "org.grails:grails-console"
    profile "org.grails.profiles:web"
    runtime "org.glassfish.web:el-impl:2.1.2-b03"
//    runtime "com.h2database:h2"
    runtime "org.apache.tomcat:tomcat-jdbc"
    runtime "com.bertramlabs.plugins:asset-pipeline-grails:2.14.4"
    testCompile "org.grails:grails-gorm-testing-support"
    testCompile "org.grails.plugins:geb"
    testCompile "org.grails:grails-web-testing-support"
    testRuntime "org.seleniumhq.selenium:selenium-htmlunit-driver:2.47.1", withoutXerces
    testRuntime "net.sourceforge.htmlunit:htmlunit:2.18", withoutXerces

    compile "org.grails.plugins:converters"
    compile 'org.grails.plugins:external-config:1.1.2'
    compile "org.grails.plugins:ala-ws-security-plugin:2.0"
    compile "org.grails.plugins:ala-ws-plugin:2.0", withoutXerces
    if(!inplace) {
        compile "org.grails.plugins:ala-auth:3.0.4"
    }
    compile "org.grails.plugins:ala-admin-plugin:2.2"

    compile "org.grails.plugins:ala-bootstrap3:3.2.1"
    compile "org.grails.plugins:quartz:2.0.12"
    compile "org.grails.plugins:mail:2.0.0.RC6"
    compile 'org.grails.plugins:grails-java8:1.2.1'
    compile 'org.grails.plugins:swagger:1.0.0'
    compile 'org.webjars:swagger-ui:2.2.8'

    // DB related
    compile group: 'org.postgresql', name: 'postgresql', version: '42.2.0'
    compile 'org.grails.plugins:postgresql-extensions:5.2.0'
    compile "org.flywaydb:flyway-core:4.2.0"
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'

    compile group: 'com.google.guava', name: 'guava', version: '23.2-jre'
    compile 'com.jakewharton.byteunits:byteunits:0.9.1'

    compile 'org.grails.plugins:aws-sdk-s3:2.1.5'
    compile 'org.javaswift:joss:0.9.17'
    compile 'org.jsoup:jsoup:1.11.3'

    testCompile "org.apache.httpcomponents:httpmime:4.3.3"
}

springBoot {
    buildInfo()
    layout = WAR
}

bootRun {
    jvmArgs('-Dspring.output.ansi.enabled=always')
    addResources = true
    System.properties.each { k,v->
        if(!systemProperties[k]) {
            systemProperties[k] = v
            println "$k = $v"
        }
    }
//    systemProperties = System.properties
}

bootRepackage {
    enabled = true
    executable = true
    classifier = 'exec'
}

assets {
    minifyJs = true
    minifyCss = true
}

war {
    exclude "xerces/**/**"
}